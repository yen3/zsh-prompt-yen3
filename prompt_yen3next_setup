#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function python-info-simple-yen3 {
  local virtualenv_format=""
  local virtualenv_formatted=""

  zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
  if [[ -z "${virtualenv_format}" ]]; then
    virtualenv_format="[%v]"
  fi

  # Clean up previous $python_info.
  unset python_info
  typeset -gA python_info

  if [[ -n "${VIRTUAL_ENV}" ]]; then
    export VIRTUAL_ENV_DISABLE_PROMPT=1
    zformat -f virtualenv_formatted "$virtualenv_format" "v:${VIRTUAL_ENV:t}"
  fi

  if [[ -n "${CONDA_DEFAULT_ENV}" ]]; then
    zformat -f virtualenv_formatted "$virtualenv_format" "v:${CONDA_DEFAULT_ENV:t}"
  fi

  if [[ -z "${virtualenv_formatted}" ]]; then
    # Get pyenv setting
    if (( $+commands[pyenv] )); then
      local pyenv_global_version=$(pyenv global)
      local pyenv_version="${"$(pyenv version)"%% *}"

      if [[ "${pyenv_version}" != "${pyenv_global_version}" ]] && \
        [[ "${pyenv_version}" != "system" ]]; then
        zformat -f virtualenv_formatted "$virtualenv_format" "v:${pyenv_version:t}"
      fi
    fi
  fi

  if [[ -n "${virtualenv_formatted}" ]]; then
    python_info[virtualenv]="${virtualenv_formatted}"
  fi
}

function prompt_yen3next_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="on ${_prompt_yen3next_colors[1]}%b%f%u%c${_prompt_yen3next_colors[4]}±"
  else
    branch_format="on ${_prompt_yen3next_colors[1]}%b%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  # Get python virtualenv name
  python-info-simple-yen3
}

function prompt_yen3next_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_yen3next_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_yen3next_colors=(
      "%F{67}"  # SteelBlue
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_yen3next_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="${_prompt_yen3next_colors[1]}%b%f%u%c"
  local action_format="${_prompt_yen3next_colors[5]}%a%f"
  local unstaged_format="${_prompt_yen3next_colors[2]}±%f"
  local staged_format="${_prompt_yen3next_colors[5]}±%f"

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format '$'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '[%v]'

  # Define prompts.
  PROMPT="%(?..%F{red}%B-> [%?]%b%f
)%F{yellow}%~%f [%n%f@%m%f] [%T] "'${vcs_info_msg_0_} ${_prompt_yen3next_colors[2]}$python_info[virtualenv]%f'"
"'$ '
  RPROMPT=''
}

function prompt_yen3next_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'yen3next'
}

prompt_yen3next_setup "$@"

# vim: ft=zsh
